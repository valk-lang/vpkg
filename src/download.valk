
use valk:http
use valk:fs
use valk:core

fn cdn_host() String {
    return "https://files.valk-cdn.dev"
}

fn get_os() String {
    return #STR(OS)
}

fn get_arch() String {
    return #STR(ARCH)
}

fn get_pack_ext() String {
    #if OS == win
    return "zip"
    #else
    return "tar.gz"
    #end
}

fn download_and_unzip_version(v: String) {
    let vdir = version_dir(v)
    if !fs:exists(vdir) : fs:mkdir(vdir) ! error("Failed to create directory: " + vdir)
    let to_path = downloads_dir() + "valk-"+ v +"-" + get_os() + "-" + get_arch() + "." + get_pack_ext()
    download_version(v, to_path)
    // Unpack
    println("# Unpacking files to: " + vdir)
    #if OS == win
    let cmd = "tar -xzf \"" + to_path + "\" -C \"" + vdir + "\""
    cmd = cmd.replace("\\", "/")
    core:exec(cmd) _
    #else
    core:exec("tar -xzf \"" + to_path + "\" -C \"" + vdir + "\"") _
    #end
}

fn download_version(v: String, to_path: String) {

    if fs:exists(to_path) : return

    let url = host() + "/api/download/url/" + get_os() + "/" + get_arch() + "/" + v
    let resp = http:request("GET", url) ! error("Failed to fetch download URL from: " + url)
    if resp.status == 404 : error("Version does not exist: " + v)
    if resp.status != 200 : error("Invalid response from: " + url)
    url = resp.body

    println("# Downloading: " + url)

    let tmp_path = to_path + ".tmp"

    let options = http:Options {
        output_to_file: tmp_path
    }

    let req = http:create_request("GET", url, options) ! {
        if E == E.invalid_url : error("Invalid URL: " + url)
        error("Failed to download: " + url)
    }

    while req.progress() ! {
        print("\n")
        println(req.recv_buffer)
        match E {
            E.disconnect => error("Failed to download: " + url + " | " + EMSG)
            default => error("Invalid response from: " + url + " | " + EMSG)
        }
    } {
        print("\r# Download progress: " + req.recv_percent + "%")
    }
    print("\n")
    println("# Download complete: " + to_path)

    fs:move(tmp_path, to_path) _
}
