
use valk:fs
use valk:core
use valk:ansi

fn help_intro_use() {
    println(emo("ðŸ’¿", "#") + " Valk version control");
    println("");
    println(" vpkg use 0.0.1       Install valk version 0.0.1");
    println(" vpkg use latest      Install latest version");
    println(" vpkg use             Install valk version defined in project valk.json");
    println("                      Expected config: { \"valk-version\": \"0.0.1\" }");
}

fn help_cmd_use(code: i32, msg: ?String (null)) $exit {

    if isset(msg) {
        println(msg)
        println("-----------------")
    }

    help_intro_use()

    exit(code)
}

fn cmd_use(args: Array[String], options: Array[String]) {

    if options.contains("-h") || options.contains("--help") : help_cmd_use(0)

    let vv = get_versions()

    let version = args.get(0) !? get_version_from_config()

    if version == "latest" {
        let valk_latest = vv.valk
        if !isset(valk_latest) : error("vpkg was unable to find out what the latest valk version is")
        version = valk_latest
    }

    if !is_valid_version(version) : error("Invalid version syntax: '" + version + "'")

    check_vpkg_versions(vv)

    if !is_installed(version) {
        // Download version
        println("# Installing version: " + version)
        download_and_unzip_version(version)
    }

    // Link
    link_version(version_dir(version))
    //
    if ansi:supported() : println("# Switched to version: " + version.ansi.green())
    else : println("# Switched to version: " + version)
}

fn is_installed(version: String) bool {
    #if OS == win
    let path = version_dir(version) + "valk.exe"
    #else
    let path = version_dir(version) + "valk"
    #end
    return fs:exists(path)
}

fn link_version(version_dir: String) {
    let to = version_dir + valk_exe()
    let link = bin_dir() + valk_exe()

    // Check target
    if !fs:exists(to) : error("Tried to create a symbolic link to a file that does not exist: " + to)

    // Remove existing link
    if fs:exists(link) : fs:remove(link) ! warn("Failed to remove previous link: " + link)

    // Create link
    fs:symlink(link, to, false) ! {
        if E == E.permissions : error("Failed to create symlink | Admin privileges required")
        if E == E.exists : error("Failed to create symlink | File already exists")
        error("Failed to create symlink")
    }

    // Message
    if ansi:supported() : println("# Link '" + link.ansi.blue() + "' to '" + to.ansi.blue() + "'")
    else : println("# Link '" + link + "' to '" + to + "'")
}
