
use valk:ansi
use valk:fs

fn help_intro_pkg_commands() {
    println(emo("ðŸ“¦", "#") + " Package commands");
    print("\n")
    println("  vp install         Install pacakages defined in the valk.json config");
    println("  vp install {pkg}   Install pacakage");
    println("  vp remove {pkg}    Remove pacakage");
}

fn help_pkg_command(code: i32) $exit {
    println(emo("ðŸ“¦", "#") + " Package commands");
    print("\n")
    println("# Install pacakages defined in the valk.json config");
    println("> vp install");
    print("\n")
    println("# Install a pacakage");
    println("> vp install {pkg} [{version}] [{alias}]");
    print("\n")
    println("- Example: Install latest version of a package");
    println("> vp install github.com/valk-lang/pkg-example");
    print("\n")
    println("- Example: Install a specific version");
    println("> vp install github.com/valk-lang/pkg-example 0.0.1");
    print("\n")
    println("- Example: Install latest version that matches 0.3.x");
    println("> vp install github.com/valk-lang/pkg-example 0.3.x");
    print("\n")
    println("# Remove a pacakage");
    println("> vp remove {pkg}");
    print("\n")
    exit(code)
}


fn cmd_package_install(args: Array[String], options: Array[String]) {

    if options.contains("-h") || options.contains("--help") {
        help_pkg_command(0)
    }

    // Project
    let p = Project.new()

    // Source
    let install_pkg_name = args.get(0) !? null
    if isset(install_pkg_name) {
        // Version to install
        let install_version = args.get(1) !? null
        let alias = args.get(2) !? null
        // Package
        let pkg = Package.init(p, install_pkg_name, install_version)

        p.packages.set(pkg.id, pkg)

        // Check if installed
        if !pkg.installed() : pkg.install()

        // Add to config
        p.add_pkg_to_config(pkg, alias)

        // Success
        if ansi:supported() : println("# Package installed: " + pkg.id.to(String).ansi.green())
        else : println("# Package installed: " + pkg.id)
        return
    }

    // Install all
    p.install_packages()
}

fn cmd_package_remove(args: Array[String], options: Array[String]) {

    if options.contains("-h") || options.contains("--help") {
        help_pkg_command(0)
    }

    // Project
    let p = Project.new()

    let name = args.get(0) ! help_pkg_command(1)

    let deps = p.config.get("dependencies")
    if !deps.has(name) {
        warn("Your project has no dependency called '%name'")
        return
    }

    deps.remove(name)
    fs:write(p.config_path, p.config.encode(true)) ! error("Failed to save file: " + p.config_path)
}