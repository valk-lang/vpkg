
use valk:ansi

fn emo(emo: String, alt: String) String {
    return ansi:supported() ? emo : alt
}

fn main(args: Array[String]) {

    let options = extract_options(args)

    args.remove(0)
    let action = args.get(0) ! help(0)
    args.remove(0)

    if action == "use" {
        cmd_use(args, options)
    } else if action == "init" {
        Project.init_config()
    } else if action == "install" {
        cmd_package_install(args, options)
    } else if action == "remove" {
        cmd_package_remove(args, options)
    } else if action == "self-update" {
        self_update()
    } else if action == "version" {
        println(#STR(VERSION))
    } else {
        println("Unknown command: '" + action + "'")
        exit(1)
    }
}

fn help(code: i32) $exit {

    println("-------------------------------")
    println("  Valk Manager " + #STR(VERSION))
    println("-------------------------------")

    // Packages
    print("\n")
    println("  vman init           Init valk.json config")

    // Packages
    print("\n")
    help_intro_pkg_commands()
    // Use
    print("\n")
    help_intro_use()
    // Toolchains
    print("\n")
    help_intro_toolchain()
    // Other
    print("\n")
    println(emo("⚙️", "#") + " vman commands");
    print("\n")
    println("  vman self-update     Update vman itself");
    println("  vman version         Show vman version");
    println("  vman {command} -h    Show help for a command");

    print("\n")

    exit(code)
}


fn extract_options(args: Array[String]) Array[String] {
    let options = array[String]{}
    each args as arg, i {
        if arg.starts_with("-") {
            options.push(arg)
            args.remove(i--)
        }
    }
    return options
}